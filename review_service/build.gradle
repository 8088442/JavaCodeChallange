plugins {
    id 'org.springframework.boot'
    id 'com.google.cloud.tools.jib'
}

group = parent.group
version = parent.version

dependencies {
    // Spring Boot dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Configuration processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Mapstruct
    implementation 'org.mapstruct:mapstruct'
    annotationProcessor 'org.mapstruct:mapstruct-processor'

    // Datastore dependencies
    runtimeOnly 'org.postgresql:postgresql'

    // Common module
    implementation project(":commons")

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'net.datafaker:datafaker'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true',
            '-Amapstruct.verbose=true',
            '-Amapstruct.unmappedTargetPolicy=ERROR',
            '-Amapstruct.unmappedSourcePolicy=WARN'
    ]
}

jib.from.image = "openjdk:17-alpine"
jib.container.jvmFlags = ["-XX:InitialRAMPercentage=75","-XX:MaxRAMPercentage=75","-XX:MaxMetaspaceSize=256m","-Dfile.encoding=UTF-8","-Dspring.profiles.active=compose"]
